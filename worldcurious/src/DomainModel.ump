namespace model;

// Location represents a location, just in terms of political distinctions
// treat like an abstract class
// all locations will have an economy
// TODO: all locations will have a population; treat as aggregate since a billion individuals would be pointless and expensive to simulate
class Location
{
  unique String name;
  lazy Integer population;
  
  1 location -- 1 Economy economy;
}

// Faction represents a nation, state, or any geopolitical entity that geopolitically acts as a unit
// it will have at least one region
// it will have at least one city, one of which will be its capital
// it will have a government
// TODO: it will have a military
// it will have trade partners, allies, and rivals
// TODO: it will have characters
class Faction
{
  isA Location;
  
  1 faction -- * Region regions;
  0..1 -> 1 City capital;
  1 faction -- * City cities;
  * tradePartners -- * Faction tradePartners;
  * allies -- * Faction allies;
  * rivals -- * Faction rivals;
}

// Region represents a geographical region
// it will be part of a faction
// it will have at least one city, one of which will be its capital
// it will have a certain climate
class Region
{
  isA Location;
  
  0..1 -> 1 City capital;
  1 region -- * City cities;
}

// City represents, well, a city
// it will be part of a region part of a faction
// it can have trade routes that connect it to other cities
class City
{
  isA Location;
  
}

// Government represents the national government
// it has a power structure, which describes how centralized government authority is
// it has a power source, which describes who/what is the source of the power, i.e., one, a few, or the people
// it has one or more power ideologies, which describes what limits are placed on power
// TODO: it can have a leader, who is a character?
// TODO: it can have several other characters in government positions?
class Government
{
  autounique id;
  enum PowerStructure { Anarchy, Confederation, Federation, UnitaryState }
  lazy PowerStructure powerStructure;
  enum PowerSource { Autocracy, Democracy, Oligarchy }
  lazy PowerSource powerSource;
  enum PowerIdeology { Constitutional, Monarchy, Republic }
  lazy PowerIdeology[] powerIdeologies;
  1 government -- 1 Faction faction;
}

// TODO: replace Person with Population, and create an association class with labor to represent the proportion in each profession
class Person
{
  autounique id;
  const Integer CALORIES_PER_YEAR = 1000000;
  
  * members -- 1 Labor job;
}

// Character represents a named person; they could be important to the story or not, but they are named
// TODO: they can have a profession
class Character
{
  isA Person;
  String name;
  Boolean isAlive;
  enum Gender { Male, Female, Other }
  Gender gender;
  lazy Date birthdate;  
  
  // physicality
  enum Build { Light, Average, Strong }
  lazy Build build;
  enum Weight { Slim, Average, Heavy }
  lazy Weight weight;
  enum Height { Short, Average, Tall }
  lazy Height height;
  // TODO: more attributes, something with personality, something with relationships and family
}

// Economy represents the sum of all the industries within it, and its output is the sum of all their outputs
// it has a number of industries
// it can have a number of trade routes
class Economy
{
  
  * economies -- * Industry industries;
}

// Industry represents an entity that converts one set of inputs into another set of outputs
// it has a number of complementary resource inputs (all of them are needed)
// it has a number of substitute resource input sets (just need one of them)
// it has a number of resource outputs
class Industry
{
 String name;
  
 * consumers -- * Resource complementaryInputs;
 * producers -- * Resource outputs;
 * industries -- * SubstituteResourceSet substituteInputSet;
  // TODO: handle complementary inputs (you need all of them) vs substitute inputs (you only need at least one of them)
}

// Military is a subclass of Industry that has no outputs
// TODO: has an associated faction
class Military
{
  isA Industry;
  
  // TODO: attributes
}

// SubstituteResourceSet is used to represent a set of Resources of which any one may satisfy a particular industry's input requirement
// e.g., a textile industry might have a SubstituteResourceSet including wool, cotton, flax, etc.; any one of these would be sufficient
class SubstituteResourceSet
{
  autounique id;
  
  * -- * Resource substituteInputs;
}

// Resource is anything — land, labor, capital, or something else — that is used to create more goods or is a final good
// TODO: more subclasses of Resource, to represent important categories of resources that would have distinct attributes
class Resource
{
  unique String name;
  // TODO: more attributes
}

// BaseResource is a subclass of Resource that represents the base resources an economy possesses without even any industries present
class BaseResource
{
  isA Resource;
  
  * baseResources -- * Economy economies;
}

// Crop is a subclass of Resource that represents agricultural crops, whether for food or for other crop derivatives
// each instance has a crop type, which represents whether it's used for food, other purposes, or both
class Crop
{
  enum CropType { FoodCrop, CashCrop, FoodAndCashCrop }
  CropType cropType;
  Integer kcalPerAcre;
  isA Resource;
  // TODO: attributes
}

// Labor is a subclass of Resource that can represent any particular profession or trade
// has a labor type, which represents what level of skill or education the profession is
// TODO: relate to population better than via Person; want to eliminate Person class
class Labor
{
  isA Resource;
  enum LaborType { ManualLabor, UnskilledLabor, SkilledLabor, EducatedLabor }
  LaborType laborType;
  // TODO: more attributes
}

// Livestock is a subclass of Resource that, like Crop represents crops, represents any type of livestock
// it has one or more uses that describe what the livestock can be used for
class Livestock
{
  isA Resource;
  // TODO: more attributes
  enum LivestockUse { Meat, Dairy, Eggs, Labor, Fiber, Other }
  LivestockUse[] livestockUses;
}

// Material is a subclass of Resource that represents any raw or processed material that goes into the production of other goods
// TODO: lots
class Material
{
  isA Resource;
}

// TODO: Food class?
// TODO: FinalGood class?

// TradeRoute represents a, well, trade route
// it has one or more economies it belongs to
// it has multiple trade exchanges, each one in a city, and each one with imported and exported resources
class TradeRoute
{
  autounique id;
  
  * tradeRoutes -- 1..* Economy economies;
}

// TradeExchange represents the import and export market in a certain city for a specific trade route
// there is one trade exchange for each pair of a city and a trade route, i.e., a city will have separate exchanges corresponding to each trade route
// it has imports that are treated as relative to the city; i.e., if a city is net buying a good, it counts as an import in that exchange
class TradeExchange
{
  autounique id;
  
  * destinations -- 1 TradeRoute tradeRoute;
  * markets -- 1 City city;
  * importMarkets -- * Resource imports;
  * exportMarkets -- * Resource exports;
}

// TODO: non-character people? i.e., they make up society and the economy, but aren't relevant to the plot
// TODO: enums for climate that can be used by a region and for crops